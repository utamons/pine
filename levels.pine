//@version=5
indicator("Equal Highs and Lows Line Drawer", shorttitle="Levels", overlay=true, max_bars_back=960)

checkRange = input.int(10, title="Check Range", minval=0)
luft = input.float(0.01, title="Luft", minval=0, step=0.01)
touches = input.int(4, title="Touches", minval=2)
exactTouches = input.int(2, title="Exact Touches", minval=0)
numberOfLevels = input.int(5, title="Number of Levels", minval=1)

var float[] highs = array.new_float()
var float[] lows = array.new_float()
var line[] lines = array.new_line()
var label[] labels = array.new_label()

shouldDrawLine(priceLevel) =>
    countLuft = 0
    countExact = 0
    for i = 0 to array.size(highs) - 1
        if math.abs(array.get(highs, i) - priceLevel) <= luft
            countLuft := countLuft + 1
        if array.get(highs, i) == priceLevel
            countExact := countExact + 1
    for i = 0 to array.size(lows) - 1
        if math.abs(array.get(lows, i) - priceLevel) <= luft
            countLuft := countLuft + 1
        if array.get(lows, i) == priceLevel
            countExact := countExact + 1
    (luft > 0 and countExact >= exactTouches and countLuft >= touches) or (luft == 0 and countLuft >= touches)

countExactHighTouches(priceLevel) =>
    countExact = 0
    for i = 0 to array.size(highs) - 1
        if array.get(highs, i) == priceLevel
            countExact := countExact + 1
    countExact

countExactLowTouches(priceLevel) =>
    countExact = 0
    for i = 0 to array.size(lows) - 1
        if array.get(lows, i) == priceLevel
            countExact := countExact + 1
    countExact

drawLine(priceLevel, lineColor, style) =>
    // Remove the oldest line if we exceed the number of levels
    if array.size(lines) == numberOfLevels
        line.delete(array.shift(lines))
    l = line.new(bar_index - 150, priceLevel, bar_index, priceLevel, color=lineColor, style = style, width=1)
    array.push(lines, l)

drawLabel(priceLevel, value, color, shift) =>
    if array.size(labels) == numberOfLevels
        label.delete(array.shift(labels))
    l = label.new(bar_index, priceLevel+shift, str.tostring(value,'#'), textcolor = color, style = label.style_none)    
    array.push(labels, l)

array.push(highs, high)
array.push(lows, low)

if (array.size(highs) > checkRange)
    array.shift(highs)
    array.shift(lows)

// Check for matching highs and lows and draw a line
if (array.size(highs) > 0)
    color = color.blue
    style = line.style_dashed
    drawHigh = shouldDrawLine(high)
    drawLow = shouldDrawLine(low)
    exHH = countExactHighTouches(high)
    exLH = countExactLowTouches(high)
    exHL = countExactHighTouches(low)
    exLL = countExactLowTouches(low)
    if (exHH > exLH and drawHigh)
        color := color.red
        style := line.style_solid
    if (exHH < exLH and drawHigh)
        color := color.green
        style := line.style_solid
    if (exLL > exHL and drawLow)
        color := color.green
        style := line.style_solid
    if (exLL < exHL and drawLow)
        color := color.red
        style := line.style_solid
     
    if drawHigh
        drawLine(high, color, style)
        drawLabel(high, (exHH+exLH), color, 0.02)
    if drawLow
        drawLine(low, color, style)
        drawLabel(low, (exHL+exLL), color, -0.06)